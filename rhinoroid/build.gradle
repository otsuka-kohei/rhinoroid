/*
Commands:
Build AAR for release: gradlew assembleRelease
Push to GitHub Packages: gradlew assembleRelease publish
 */

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'

    id 'maven-publish'

    id 'org.jetbrains.dokka'
}

ext {
    versionName = "0.4"
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'org.mozilla:rhino:1.7.14'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = 'https://maven.pkg.github.com/otsuka-kohei/rhinoroid'

            credentials {
                def localProperties = new Properties()
                localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

                username = localProperties.containsKey("com.github.user") ? localProperties.getProperty('com.github.user') : ''
                password = localProperties.containsKey("com.github.token") ? localProperties.getProperty('com.github.token') : ''
            }
        }
    }

    publications {
        GitHubPackages(MavenPublication) {
            groupId "com.github.otsuka_kohei"
            artifactId "rhinoroid"
            version versionName

            afterEvaluate {
                from components.release
            }

            pom {
                url = 'https://github.com/otsuka-kohei/rhinoroid'

                licenses {
                    license {
                        name = 'Apache License Version 2.0'
                    }
                }
            }
        }
    }
}